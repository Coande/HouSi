apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

def gitCommit = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    signingConfigs {
        housi_release {
            keyAlias project.keyAlias
            keyPassword project.keyPassword
            storeFile file(project.storeFile)
            storePassword project.storePassword
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId project.APPLICATION_ID
        minSdkVersion 16
        targetSdkVersion 26
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.housi_release

        buildConfigField "String", "BUILD_TIME", "\"${System.currentTimeMillis()}\""
        buildConfigField "String", "GIT_COMMIT", "\"${gitCommit}\""
        buildConfigField "String", "SOURCE_KEY", project.source_key
        buildConfigField "String", "DEFAULT_HOSTS_URL", project.default_hosts_url
        buildConfigField "String", "FAQ_URL", project.faq_url
        buildConfigField "String", "MY_GITHUB_URL", project.my_github_url
        manifestPlaceholders = [com_google_android_backup_api_key: project.com_google_android_backup_api_key,
                                pgyer_appid                      : project.pgyer_appid]
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            debuggable false
//            shrinkResources = true
            signingConfig signingConfigs.housi_release
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.housi_release
        }
    }
    flavorDimensions "default"
    productFlavors {
        housi {
            dimension "default"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
        housiForce {
            dimension "default"
            versionName project.VERSION_NAME
            versionNameSuffix '(force)'
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    }
    android.applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.outputs.all { output ->
                outputFileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}" +
                        "_${gitCommit}_${variant.buildType.name}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    //        exclude group: 'com.android.support', module: 'support-annotations'
    //    })
    //    testCompile 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:percent:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:customtabs:26.1.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'io.reactivex:rxjava:1.3.2'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.qiangxi.checkupdatelibrary:checkupdatelibrary:1.1.3@aar'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'
    implementation 'com.pgyersdk:sdk:2.7.0'
    implementation 'com.coolerfall:android-app-daemon:1.2.0'
    implementation 'eu.chainfire:libsuperuser:1.0.0.201704021214'
    //    implementation 'com.takisoft.fix:preference-v7:27.0.0.0'
    //    implementation 'com.takisoft.fix:preference-v7-extras:27.0.0.0'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.3-support26.1.0'
    implementation('com.mikepenz:aboutlibraries:5.9.7@aar') {
        transitive = true
        exclude group: 'com.android.support'
    }
    implementation 'com.mikepenz:iconics-core:2.9.0@aar'
    implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    implementation 'com.mikepenz:community-material-typeface:1.7.22.1@aar'
    //    compile 'com.google.firebase:firebase-messaging:11.0.4'
    //    compile 'com.google.android.gms:play-services:11.0.4'
    implementation 'moe.shizuku.preference:preference:2.0.8'
    implementation 'moe.shizuku.preference:preference-dialog-appcompat:2.0.8'
    implementation 'moe.shizuku.preference:preference-simplemenu:2.0.8'
}

//apply plugin: 'com.google.gms.google-services'